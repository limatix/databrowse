<xsl:template xmlns:imgdir="http://thermal.cnde.iastate.edu/databrowse/imgdir" match="imgdir:imgdir" mode="detailed">
    <xsl:call-template name="db_directory_image-directorytree">
        <xsl:with-param name="mode">detailed</xsl:with-param>
    </xsl:call-template>
</xsl:template>
<xsl:template xmlns:imgdir="http://thermal.cnde.iastate.edu/databrowse/imgdir" match="imgdir:imgdir" mode="summary">
    <xsl:call-template name="db_directory_image-directorytree">
        <xsl:with-param name="mode">summary</xsl:with-param>
    </xsl:call-template>
</xsl:template>
<xsl:template xmlns:imgdir="http://thermal.cnde.iastate.edu/databrowse/imgdir" match="imgdir:imgdir" mode="title">
    <xsl:call-template name="db_directory_image-directorytree">
        <xsl:with-param name="mode">title</xsl:with-param>
    </xsl:call-template>
</xsl:template>
<xsl:template xmlns:imgdir="http://thermal.cnde.iastate.edu/databrowse/imgdir" name="db_directory_image-directorytree">
    <xsl:param name="mode"/>
    <xsl:if test="@root='True' and not(@ajaxreq='True')">
        <script type="text/javascript" DEFER="true">
                <![CDATA[
                    $.ImgToggle = function(img, resurl) {
                        if(img.attr("src") == resurl + "/icons/folder-open.png") {
                            img.attr("src", resurl + "/icons/folder.png");
                        }
                        else {
                            img.attr("src", resurl + "/icons/folder-open.png");
                        }
                    };
                    $.AjaxRequest = function(id, img, requrl, resurl) {
                        if(img.attr("src") == resurl + "/icons/folder-open.png"){
                            img.attr("src", resurl + "/icons/folder.png");
                        }
                        else if(id.html() != "") { 
                            img.attr("src", resurl + "/icons/folder-open.png");
                        }
                        else {
                            $.ajax({
                                url: requrl,
                                dataType: "xml",
                                beforeSend: function ( xhr ) {
                                    img.attr("src", resurl + "/icons/loading.gif");
                                    $("#loading").fadeIn(100);
                                }
                            }).done(function ( data ) {
                                text = $(data).find('ul.rootdir')
                                id.html(text);
                                img.attr("src", resurl + "/icons/folder-open.png");
                                $("#loading").fadeOut();
                            });
                        }
                    };
                ]]> 
        </script>
        <style>
                ul.tree, ul.tree ul {
                    list-style-type: none;
                    margin: 0px;
                    padding: 0 0px;
                    background: url('<xsl:value-of select="$resdir"/>/vline.png') repeat-y;
                }
                .item {
                    margin: 0px;
                    padding: 0 18px;
                    line-height:20px;
                    background: url('<xsl:value-of select="$resdir"/>/node.png'), url('<xsl:value-of select="$resdir"/>/vline.png');
                    background-repeat: no-repeat, repeat-y;
                }
                ul.tree li:last-child{
                    background: #fff url('<xsl:value-of select="$resdir"/>/lastnode.png') no-repeat;
                }
                ul.root {
                    margin: 0px;
                    padding: 0px;
                    list-style-type: none;
                    margin-bottom: 0px;
                    background: none;
                }
                ul.tree ul.rootdir {
                    padding: 0 0px;
                }                
                li.subdir {
                    margin: 0px;
                    padding: 0 0px;
                    line-height:20px;
                    background: #fff none;
                }
                ul.tree li.subdir {
                    background: #fff none;
                }                
                ul.tree li.rootdir {
                    background: #fff none;
                }
                .icon {
                    padding-right: 4px;
                }
                .basename {
                    font-weight: bold;
                    padding-right: 4px;
                }
                .title {
                    padding-right: 4px;
                }
        </style>
    </xsl:if>
    <xsl:choose>
         <xsl:when test="@root='True'">
            <ul class="tree root" id="tree">
                <xsl:call-template name="db_directory_image-treestructure">
                    <xsl:with-param name="mode"><xsl:value-of select="$mode"/></xsl:with-param>
                </xsl:call-template>
            </ul>
         </xsl:when>
         <xsl:otherwise>
            <ul class="tree root">
                <xsl:call-template name="db_directory_image-treestructure">
                    <xsl:with-param name="mode"><xsl:value-of select="$mode"/></xsl:with-param>
                </xsl:call-template>
            </ul>
         </xsl:otherwise>
    </xsl:choose>
</xsl:template>
<xsl:template xmlns:imgdir="http://thermal.cnde.iastate.edu/databrowse/imgdir" name="db_directory_image-treestructure">
    <xsl:param name="mode"/>
    <li>
        <xsl:choose>
            <xsl:when test="@root='True'">
                <xsl:attribute name="class">rootdir</xsl:attribute>
                <span class="icon">
                    <img>
                        <xsl:attribute name="alt"><xsl:value-of select="@path"/></xsl:attribute>
                        <xsl:attribute name="title"><xsl:value-of select="@path"/></xsl:attribute>
                        <xsl:attribute name="src"><xsl:value-of select="$resdir"/>/icons/folder-visiting.png</xsl:attribute>
                    </img>
                </span>
                <span class="basename">
                    <span style="overflow:hidden;max-width:670px;display:inline-block;text-overflow:ellipsis;white-space:nowrap"><xsl:value-of select="@path"/></span>&#160;
                    <a>
                        <xsl:attribute name="href"><xsl:value-of select="@uphref"/></xsl:attribute>
                        <img alt="Up" title="Go Up One Directory Level">
                            <xsl:attribute name="src"><xsl:value-of select="$resdir"/>/icons/up.png</xsl:attribute>
                        </img>
                    </a>
                </span>
            </xsl:when>
            <xsl:when test="@ajax='True'">
                <xsl:attribute name="class">subdir</xsl:attribute>
                <span class="icon">
                    <img onclick="$(this).parent().next().next().toggle();$.AjaxRequest($(this).parent().next().next(),$(this),&apos;{@ajaxurl}&apos;,&apos;{$resdir}&apos;);">
                        <xsl:attribute name="alt"><xsl:value-of select="@path"/></xsl:attribute>
                        <xsl:attribute name="title"><xsl:value-of select="@path"/></xsl:attribute>
                        <xsl:attribute name="src"><xsl:value-of select="$resdir"/>/icons/folder-pictures.png</xsl:attribute>
                    </img>
                </span>
                <span class="basename">
                    <a>
                        <xsl:attribute name="href"><xsl:value-of select="@href"/></xsl:attribute>
                        <xsl:value-of select="@name"/>
                    </a>
                </span>
            </xsl:when>
            <xsl:otherwise>
                <xsl:attribute name="class">subdir</xsl:attribute>
                <span class="icon">
                    <img onclick="$(this).parent().next().next().toggle();$.ImgToggle($(this),&apos;{$resdir}&apos;);">
                        <xsl:attribute name="alt"><xsl:value-of select="@path"/></xsl:attribute>
                        <xsl:attribute name="title"><xsl:value-of select="@path"/></xsl:attribute>
                        <xsl:attribute name="src"><xsl:value-of select="$resdir"/>/icons/folder-pictures.png</xsl:attribute>
                    </img>
                </span>
                <span class="basename">
                    <a>
                        <xsl:attribute name="href"><xsl:value-of select="@href"/></xsl:attribute>
                        <xsl:value-of select="@name"/>
                    </a>
                </span>
            </xsl:otherwise>
        </xsl:choose>
        <ul>
            <xsl:choose>
                <xsl:when test="@root='True'">
                    <xsl:attribute name="class">rootdir</xsl:attribute>
                </xsl:when>
                <xsl:otherwise>
                    <xsl:attribute name="class">subdir</xsl:attribute>
                    <xsl:attribute name="style">display:none</xsl:attribute>
                </xsl:otherwise>
            </xsl:choose>
            <xsl:for-each select="imgdir:file">
                <li class="item">
                    <xsl:if test="not(child::imgdir:imgdir)">
                        <span class="icon">
                            <img>
                                <xsl:attribute name="title"><xsl:value-of select="@basename"/></xsl:attribute>
                                <xsl:attribute name="src"><xsl:value-of select="$resdir"/>/icons/<xsl:value-of select="@icon"/></xsl:attribute>
                            </img>
                        </span>
                        <span class="basename">
                            <a>
                                <xsl:attribute name="href"><xsl:value-of select="@link"/></xsl:attribute>
                                <xsl:value-of select="@basename"/>
                            </a>
                        </span>
                    </xsl:if>
                    <xsl:choose>
                        <xsl:when test="$mode='title'">
                            <xsl:apply-templates select="." mode="title"/>
                        </xsl:when>
                        <xsl:when test="$mode='summary'">
                            <xsl:apply-templates select="." mode="summary"/>
                        </xsl:when>
                        <xsl:when test="$mode='detailed'">
                            <xsl:apply-templates select="." mode="detailed"/>
                        </xsl:when>
                    </xsl:choose>          
                </li>
            </xsl:for-each>
        </ul>
    </li>
</xsl:template>
